---
layout: post
title: "ZeroOS之引导扇区"
description: "ZeroOs Boot 阶段"
category: 操作系统
tags: [操作系统,引导扇区]
---

##BIOS的启动原理

在开机加电的一瞬间，内存当中什么程序都没有，我们不可能认为地去执行BIOS程序。既然软件的方法不能去执行BIOS，那么就只能靠硬件来执行了。

Intel的所有80x86的CPU的硬件都设计为加电即进入16为的实模式状态运行。同时CPU的硬件逻辑在加点瞬间强制设置CS的值为0xFFFF，IP的值为0x0000，这样CS:IP就指向了0xFFFF0这个地址。

BIOS程序的入口地址就是在0xFFFF0。之后BIOS程序会进行检查硬件等操作，其中最关键的就是要建立中断向量表和中断服务程序。

最后BIOS程序会把软盘的第一扇区加载到内存当中0x07C00这个位置，然后控制权就交给加载进来的这个程序了。

PS:

1. 我这里用的是软盘启动，所以会加载软盘的第一个扇区的数据到内存中。
2. 中断这个概念在操作系统中非常重要，不懂的请自行Google。
3. CS(Code Segment Register): 代码段寄存器，存在CPU中，指向CPU当前执行代码在内存中所在的区域。IP(Instruction Pointer): 指令指针寄存器，记录将要执行的指令在代码段内的偏移地址。

##Boot.s 引导扇区

软盘的第一个扇区就是软盘的引导扇区，也就是BIOS要加载的扇区。由于引导扇区只有512字节，而在进入内核之前还需要完成很多工作，所以还需要其他部分的代码来完成剩下的工作。那么这些代码就由这个启动扇区来加载进来。

这里boot.s的工作就是要加载setup.s来完成其他工作，另外把内核也加载到内存中。

首先来规划下这些文件要加载到哪。

    BaseOfSetup        equ         09000h ; setup.bin 被加载到的位置 --- 段地址
    OffsetOfSetup      equ         0100h  ; setup.bin 被加载到的位置 --- 偏移地址
    SetupLen           equ         4      ; setup.bin 的长度，4个扇区
    BaseOfKernelFile    equ         08000h ; kernel.bin 被加载到的位置 --- 段地址
    OffsetOfKernelFile  equ         0h     ; kernel.bin 被加载到的位置 --- 偏移地址
    KernelLen           equ         20     ; kernel.bin 的长度，20个扇区
    BaseOfStack		    equ	        07c00h ; Boot状态下堆栈基地址(栈底, 从这个位置向低地址生长)

初始化一些寄存器：

    mov ax, cs
    mov ds, ax
    mov es, ax
    mov ss, ax
    mov sp, BaseOfStack

## 加载setup进入内存

在这里，boot.bin是写入在软盘的第1个扇区的，setup.bin是写入软盘的第2到第5个扇区，总共四个扇区，2字节。所以这里只要将第2到第5个扇区的数据读入到内存的相应位置就行了。

要读入软盘，这里就要用到BIOS中断int 13h。用法如下：

    ---------------------------------------------------
    寄存器                                用法
    ---------------------------------------------------
    ah = 00h    dl = 驱动器号(0表示A盘)    复位软驱
    ---------------------------------------------------
    ah = 02h    al = 要读扇区数            从磁盘将数据
    ch = 磁道号 cl = 起始扇区号             读入es:bx指向
    dh = 磁头号 dl = 驱动器号(0表示A盘)      的缓冲区中
    es:bx -> 输出数据缓冲区              
    ---------------------------------------------------

加载setup.bin代码:

    ; 加载 Setup.bin, 位于软驱第2到第5个扇区
    load_Setup:
        mov ax, BaseOfSetup
        mov es, ax
        mov bx, OffsetOfSetup  ; es:bx = BaseOfsetup:OffsetOfLoader
        mov ah, 02h
        mov al, SetupLen
        mov cx, 0x0002      ; sector 2, track 0 (CH=0, CL=2)
        mov dx, 0x0000      ; drive 0, head 0 (DH=0, DL=0)
        int 13h
        jnc ok_load_Setup
        xor ah, ah          ; reset the diskette
        xor dl, dl
        int 13h
        jmp load_Setup

## 加载内核进入内存

在这里，内核文件kernel.bin是位于第6到第25个扇区，占20个扇区。

加载kernel.bin的代码和上面的略有区别，原因在于，上面第2到第5个扇区都在同一磁道，同一磁头下，连续的，所以可以直接设置al为要读的扇区数4。而kernel.bin跨磁头了。所以这里采用一个一个扇区来读的策略。读完一个扇区，判断下一个扇区的位置，然后设置寄存器，继续读。

软盘的读取是先读取某一面的一个磁道上的18个扇区，读完之后读取方面那个扇区相同磁道下的另外18个扇区，之后回到另一面，读取下一个磁道的扇区。

加载kernel.bin代码:

    ; 加载 kernel.bin， 位于软驱第6到第25个扇区
    load_Kernel:
        mov ax, BaseOfKernelFile
        mov es, ax
        mov bx, OffsetOfKernelFile  ; es:bx = BaseOfKernelFile:OffsetOfKernelFile
        mov si, 0   ; 记录已读取的扇区数

        mov ch, 0   ; 柱面0
        mov dh, 0   ; 磁头0
        mov cl, 6   ; 扇区6
    .read:
        mov ah, 0x02
        mov al, 1   ; 读1个扇区
        mov dl, 0x00      ; A 驱动器
        int 13h
        add si, 1
        cmp si, KernelLen  ; 是否读了20个扇区
        je ok_load_Kernel

    .next:
        add bx, 0x200   ; 把内存地址向后移动0x200 即512字节
        add cl, 1       ; 扇区加1
        cmp cl, 18
        jbe .read       ; if cl <= 18, jump to .read
        mov cl, 1
        add dh, 1
        cmp dh, 2
        jb .read        ; if dh < 2 , jump to .read
        mov dh, 0
        add ch, 1
        cmp ch, 80
        jb .read        ; if ch < 80, jump to .read

##显示一段字符串

在加载内核之前，让我们来在屏幕上显示一句话，好告诉用户系统正在启动中。

要在屏幕上显示，就要用到BIOS中断int 10h。

int 10h 中断在这里不多说，具体的自行Google。


    ok_load_Setup:

        ; print "Booting..."
        mov ax, BootMessage
        mov bp, ax
        mov ax, ds
        mov es, ax          ; ES:BP = 串地址
        mov cx, MessageLength
        mov	ax, 01301h		; AH = 13,  AL = 01h
        mov	bx, 0007h		; 页号为0(BH = 0) 黑底白字(BL = 07h)
        mov	dl, 0
        int	10h			; int 10h

        ...

        MessageLength       equ   10
        BootMessage         db    "Booting..."  ; 10字节

##移交控制权

一个跳转命令解决，跳转到去执行 setup.bin中的代码。

    ok_load_Kernel:
        jmp BaseOfSetup:OffsetOfSetup       ; 跳转去执行 setup.bin

最后可能编译完之后boot.bin的文件没达到512字节，在boot.s最后添加上

    times 510-($-$$)  db 0
    dw 0xaa55

最后的`dw 0xaa55`是在512字节的最后两字节写入0xaa55。这是BIOS判断是否是引导扇区的标志。

OK! boot.bin的工作到此位置，后面的就交给setup.bin来搞定了。

该项目的地址在[这呢](https://github.com/waikeung/ZeroOS)！